buildscript {
    ext.kotlin_version = '1.2.0'

    repositories {
        maven {     url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'com.scorsi'
version '0.1-alpha'

apply plugin: 'kotlin'
apply plugin: 'groovy'
apply plugin: 'application'

mainClassName = "horilang.Main"

repositories {
    maven {     url 'http://dl.bintray.com/kotlin/kotlin-eap-1.2' }
    mavenCentral()
}

dependencies {
    compile group: 'org.jetbrains.kotlin',
            name: 'kotlin-stdlib-jdk8',
            version: kotlin_version
    compile group: 'org.jetbrains.kotlin',
            name: 'kotlin-reflect',
            version: kotlin_version
    compile group: 'org.codehaus.groovy',
            name: 'groovy-all',
            version: '2.4.13'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    dependsOn = compileKotlin.taskDependencies.values - 'compileJava'
}

compileGroovy {
    dependsOn compileKotlin
    classpath += files(compileKotlin.destinationDir)
}

classes {
    dependsOn compileGroovy
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    baseName = "horilang"
    manifest {
        attributes (
                "Class-Path" : configurations.compile.collect { it.getName() }.join(' '),
                "Main-Class" : mainClassName
        )
    }
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    destinationDir = file("$rootDir/bin")
}

task ("package", dependsOn: jar, type: Jar) {}
